// D'Sparil himself

ACTOR Sorcerer5 : Sorcerer3 replaces Sorcerer3
{
	Tag "D'Sparil (Serpent Rider)"
	+DONTRIP
	+BOSS
    DamageFactor "BJSpear", 0.3
	DamageFactor "DCannonProj", 0.7
	DamageFactor "Tesla", 0.6
	DamageFactor "Shock", 0.3
	DamageFactor "LifeLeech", 0.8		
	DamageFactor "CalebFire", 0.7	
	DamageFactor "Vodoo", 0.2	
	DamageFactor "DisruptorWall", 0.4
	DamageFactor "EradNova", 0.3
    DamageFactor "EradGreenFire", 0.1
    DamageFactor "EradCoopGreenFire", 0.1
	DamageFactor "RottFire", 0.6
	DamageFactor "HLLaser", 0.5
	DamageFactor "WTRangedFire", 0.2
	DamageFactor "Nuke", 0.2
	DamageFactor "SWNuke", 0.2
	DamageFactor "SWNukeCoop", 0.2
	DamageFactor "SWFlame", 0.1
	DamageFactor "SWFlameCoop", 0.1
	DamageFactor "SWFlame2", 0.2
	DamageFactor "SWFlame2Coop", 0.2
	DamageFactor "SWFlame3", 0.4
    DamageFactor "RMR-BigPlasma", 0.4
	DamageFactor "CMDMGSHIT", 0.7
	DamageFactor "Disintegrate", 0.5	
	DamageFactor "FireExplosive", 0.7
	DamageFactor "C7Disintegrate", 0.5		
	States
	{
		Spawn:
			SRCR A 0
			TNT1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Super::Spawn")
			TNT1 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			TNT1 A 0 A_GiveInventory("AlreadySpawned",1)
			goto Super::Spawn

		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossMonologueFlagsDisabler")
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			SRCR ABCD 5 A_Sor1Chase
			Loop
		Pain:
			SRCR Q 1 A_Sor1Pain
			Goto See
		Missile:
			SRCR Q 0 A_Jump(256,"StrafeLeft","StrafeRight")
		StrafeLeft:
			SRCR Q 7 ThrustThing(angle*256/360+192, 12, 0, 0)
			Goto Fire
		StrafeRight:
			SRCR Q 7 ThrustThing(angle*256/360+64, 12, 0, 0)
			Goto Fire
		Fire:
			SRCR R 6 A_FaceTarget
			SRCR S 10 A_Srcr1Attack
			Goto See
		Missile2:
			SRCR S 5 A_FastChase
			SRCR Q 0 A_Jump(256,"StrafeLeft2","StrafeRight2")
		StrafeLeft2:
			SRCR Q 7 ThrustThing(angle*256/360+192, 12, 0, 0)
			Goto Fire2
		StrafeRight2:
			SRCR Q 7 ThrustThing(angle*256/360+64, 12, 0, 0)
			Goto Fire2
		Fire2:
			SRCR Q 7 A_FaceTarget
			SRCR R 6 A_FastChase
			SRCR S 10 A_Srcr1Attack
			SRCR S 5 A_FastChase
			SRCR Q 0 A_Jump(256,"StrafeLeft3","StrafeRight3")
		StrafeLeft3:
			SRCR Q 7 ThrustThing(angle*256/360+192, 12, 0, 0)
			Goto Fire3
		StrafeRight3:
			SRCR Q 7 ThrustThing(angle*256/360+64, 12, 0, 0)
			Goto Fire3
		Fire3:
			SRCR Q 7 A_FaceTarget
			SRCR R 6 A_FastChase
			SRCR S 10 A_Srcr1Attack
			TNT1 A 0 A_Jump(128,"Missile2")
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SRCR E 7 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SRCR F 7 A_Scream
			SRCR G 7
			SRCR HIJK 6
			SRCR L 25 A_PlaySound("dsparil/zap", CHAN_BODY)
			SRCR MN 5
			SRCR O 4
			SRCR L 20 A_PlaySound("dsparil/zap", CHAN_BODY)
			SRCR MN 5
			SRCR O 4
			SRCR L 12 A_NoBlocking
			SRCR P -1 A_SpawnItemEx("Sorcerer6",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERSPECIAL,0,tid)
			Stop
		// SamsaraMonsterReincDsparil1 states
		SpawnTheSerpentRiderHimself:
			TNT1 A 34 A_PlaySound("dsparil/zap", CHAN_BODY)
			TNT1 A 41 A_PlaySound("dsparil/zap", CHAN_BODY)
			TNT1 A 1 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("Sorcerer6",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERSPECIAL,0)
			Stop	
	}
}

ACTOR Sorcerer6 : Sorcerer4 replaces Sorcerer4
{
	Tag "D'Sparil"
	+DONTRIP
	+BOSS
	DeathSound "dsparil/scream"
    DamageFactor "BJSpear", 0.3
	DamageFactor "DCannonProj", 0.7
	DamageFactor "Tesla", 0.6
	DamageFactor "Shock", 0.3
	DamageFactor "LifeLeech", 0.8		
	DamageFactor "CalebFire", 0.7	
	DamageFactor "Vodoo", 0.2	
	DamageFactor "DisruptorWall", 0.4
	DamageFactor "EradNova", 0.3
    DamageFactor "EradGreenFire", 0.1
    DamageFactor "EradCoopGreenFire", 0.1
	DamageFactor "RottFire", 0.6
	DamageFactor "HLLaser", 0.5
	DamageFactor "WTRangedFire", 0.2
	DamageFactor "Nuke", 0.2
	DamageFactor "SWNuke", 0.2
	DamageFactor "SWNukeCoop", 0.2
	DamageFactor "SWFlame", 0.1
	DamageFactor "SWFlameCoop", 0.1
	DamageFactor "SWFlame2", 0.1
	DamageFactor "SWFlame2Coop", 0.1
	DamageFactor "SWFlame3", 0.4
    DamageFactor "RMR-BigPlasma", 0.4
	DamageFactor "CMDMGSHIT", 0.7
	DamageFactor "Disintegrate", 0.5	
	DamageFactor "FireExplosive", 0.7
	DamageFactor "C7Disintegrate", 0.5			
	States
	{
		Spawn:
			SOR2 L 0
			SOR2 L 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SOR2 L 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			SOR2 L 0 A_GiveInventory("AlreadySpawned",1)
			SOR2 L 0
		Spawn2:
			SOR2 LKJI 6
			SOR2 H 6 A_PlaySound("dsparil/sight", CHAN_BODY, 1.0, false, ATTN_NONE)
			SOR2 G 6
			Goto See
		Missile:
			SOR2 R 5 //A_Srcr2Decide
			SOR2 S 7 A_FaceTarget
			SOR2 T 8 A_Srcr2Attack
			TNT1 A 0 A_Srcr2Decide
			TNT1 A 0 A_Jump(64,"See")
			SOR2 S 7 A_FaceTarget
			SOR2 T 8 A_Srcr2Attack
			TNT1 AA 0 A_Srcr2Decide
			TNT1 A 0 A_Jump(96,"See")
			SOR2 S 7 A_FaceTarget
			SOR2 T 8 A_Srcr2Attack
			TNT1 AAA 0 A_Srcr2Decide
			TNT1 A 0 A_Jump(128,"See")
			SOR2 S 7 A_FaceTarget
			SOR2 T 8 A_Srcr2Attack
			TNT1 AAAAA 0 A_Srcr2Decide
			Goto See
		Rise:
			SOR2 A 0
			SOR2 A 1 A_TakeInventory("AlreadySpawned",1)
			SOR2 A 1 A_JumpIfInventory("AlreadySpawned",1,"Super::Rise")
			SOR2 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			SOR2 A 0 A_GiveInventory("AlreadySpawned",1)
			SOR2 A 0
			Goto "Super::Rise"
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SetTranslucent(1,0)
			SDTH A 8 A_KillChildren//A_Sor2DthInit
			SDTH B 8
			SDTH C 8 A_Scream
		DeathLoop:
			SDTH DEFDEFDEFDEFDEFDEFDEF 7
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			SDTH G 6 A_PlaySound("dsparil/explode", CHAN_BODY)
			SDTH H 6
			SDTH I 18
			SDTH J 6 A_NoBlocking
			SDTH K 6 A_PlaySound("dsparil/bones", CHAN_BODY)
			SDTH LMN 6
			SDTH O -1 A_KillMaster
			Stop
		Teleport:
			TNT1 A 0 A_Jump(256,"GhostOn","GhostOff")
		GhostOn:
			TNT1 A 0 A_SetTranslucent(0.5,0)
			TNT1 A 0 A_ChangeFlag("GHOST",1)
			Goto TeleportIn
		GhostOff:
			TNT1 A 0 A_SetTranslucent(1,0)
			TNT1 A 0 A_ChangeFlag("GHOST",0)
			Goto TeleportIn
		TeleportIn:
			SOR2 LKJIHG 6
			Goto See
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2ZorchDeath
		Death.DukePlasma:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2DukePlasmaDeath
		Death.RottDisintegrate:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2RottDisintegrateDeath
		Disintegrate:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2DisintegrateDeath
		Death.Nuke:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2NukeDeath
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" "#" 0 A_KillChildren
			Goto Super::Sorcerer2IceDeath
	}
}

ACTOR Sorcerer2FX2B : Sorcerer2FX2 replaces Sorcerer2FX2
{
	ReactionTime 1
	States
	{
		Spawn:
			FX11 ABABABA 5 Bright
			TNT1 A 0 ACS_NamedExecuteWithResult("Heretic_SpawnDisciple")
			Goto SpawnLoop
		SpawnLoop:
			FX11 BA 5 Bright
			Loop
		Explode:
			FX11 BBBBBAAAAA 1 Bright A_Countdown
			Loop
		Death:
			FX11 CDEFG 5 Bright
			Stop
	}
}

Actor HereticTeleportFog
{
	+NOBLOCKMAP
	+NOTELEPORT
	+NOGRAVITY
	+CLIENTSIDEONLY
	-SOLID
	RenderStyle Add
	States
	{
		Spawn:
			TEL7 ABCDEFGHGFEDC 6 Bright
			Stop
	}
}

Actor Sorcerer1FX12B : SorcererFX1 replaces SorcererFX1 { DeathSound "minotaur/fx3hit" }