Actor WaterAlien : SamsaraMonsterSkeleton {
	Health 163
	Radius 20
	Height 56
	Speed 6
	Mass 1000
	PainChance 96
	PainChance "ShrinkerDMG", 0
	Scale 1.3
	Species "Enemies"
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+LOOKALLAROUND
	+DONTRIP	
	-SHOOTABLE
	SeeSound ""
	PainSound ""
	DeathSound "wateralien/death"
	ActiveSound ""
	Tag "Water Alien"
	Obituary "%o thought %g could drink a Water Alien."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "Electric", 2.0
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "QuakeElectric", 2.0
	DamageFactor "Hexen2Electric", 2.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	BloodColor "LightBlue"
	Decal None
	States {
		Spawn:
			BS16 A 0
			BS16 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			BS16 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			BS16 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			BS16 A 5 A_Look
			Loop
		See:
			BS16 AABBCC 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BS16 DEF 4
			TNT1 A 0 A_SetShootable
			BS16 G 5 A_FaceTarget
		MissileContinue:
			BS16 HIJ 4 A_FaceTarget
			TNT1 A 0 A_CustomMissile("WaterAlienShot", 40, 8)
			BS16 G 8
			TNT1 A 0 A_UnsetShootable
			BS16 FED 4
			Goto See
		Pain:
			BS16 K 5 A_Pain
			Goto MissileContinue
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			BS16 K 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			BS16 K 16 A_Pain
			Goto MissileContinue
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			BS16 L 8 A_Scream
			BS16 M 6 A_NoBlocking
			BS16 A 0 A_KillMaster
			BS16 NO 6 
			BS16 P -1
			Stop
		}
}

Actor WaterAlienShot {
	PROJECTILE
	Speed 24
	Height 8
	Radius 5
	Damage 5
	DeathSound "wateralien/death"
	Decal None
	States {
		Spawn:
			BFX3 ABCB 3 Bright
		Loop
		Death:
			BFX3 DEF 4 Bright
		Stop
	}
}
