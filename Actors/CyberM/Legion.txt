Actor CMLoD : SamsaraMonsterCyberneticTotalGib
{ 
    Health 1150
    Radius 24
    Height 64
	Mass 1000
    Speed 8
    PainChance 30
    MONSTER
	Species "CMEnemies"	
    +FLOORCLIP
	+NOTDMATCH
//	+NOBLOOD
	+DONTRIP
	+NOINFIGHTING
    Obituary "%o was no match for the Legion of Doom."
    SeeSound "cmlod/sight"
    PainSound "cmlod/pain"
    DeathSound "cmlod/death"
    ActiveSound "cmlod/active"
	Scale 0.33
	RadiusDamageFactor 1.5
	BloodColor "Green"
	Tag "Legion of Doom"
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0	
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.0
	DropItem "CMLegionSlot5"
    States
    {
   Spawn:
		C_LD A 0
		C_LD A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		C_LD A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		C_LD A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
        C_LD A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_PlaySound ("cmlod/walk")
        C_LD AABBCCDD 4 A_Chase("","Missile",CHF_NOPLAYACTIVE)
		C_LD AABB 4 A_Chase
		TNT1 A 0 A_PlaySound ("cmlod/walk")
		C_LD CCDDAABB 4 A_Chase("","Missile",CHF_NOPLAYACTIVE)
		C_LD CCDD 4 A_Chase
        Loop
    Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
        C_LD E 5 A_FaceTarget
		TNT1 A 0 A_PlaySound ("cmlod/attack", CHAN_WEAPON)
        C_LD F 6 bright A_CustomMissile("CMRocket2", 32.0, 0.0, 0, 0)
        C_LD E 2 A_FaceTarget
        Goto See
    Pain:
        C_LD G 2
        C_LD G 5 A_Pain
        Goto See
    Death:
	    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
        C_LD H 2
        C_LD H 10 A_Scream
        C_LD I 2 A_Fall
        C_LD I 3
        C_LD I -1 A_KillMaster
        Stop
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		C_LD G 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		C_LD G 16 A_Pain
		Goto See		
	}
}

Actor CMRocket2 : CMRocket 
{ 
    Speed 20 
    Damage (random(7,16))
	States
	{
	Spawn:
		CMRK AAAABBBBCCCC 1 Bright
		Loop
	Death:
		CMX2 B 4 Bright
		CMX2 C 4 Bright A_Explode(43, 104, 1, 0)
		CMX2 D 4 Bright
		CMX2 E 4 Bright
		CMX2 F 4 Bright
		CMX2 G 4 Bright
		Stop
	}
}

actor CMLegionSlot5 : SamsaraSlotFiveDrop
{
    //Scale 0.3
    +DROPPED
    +NEVERRESPAWN
    States
    {
      Spawn:
        CMRL F -1
        stop
    }
}
