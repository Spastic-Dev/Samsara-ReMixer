ACTOR TotenkopfSchabbsMutant : SamsaraMonsterHumanoidBoss
{
	Health 1550
	Radius 32
	Height 64
	Speed 6
	Mass 1000
	PainChance 0
	+FLOORCLIP
	+NOTDMATCH
	+DONTRIP
	+THRUGHOST
	+MISSILEMORE
	+LOOKALLAROUND
	+BOSS
	Tag "Dr. Schabbs (Mutant)"
	Seesound "Totenkopf/Boss2Sight"
	Deathsound "Totenkopf/Boss2Death"
	Activesound ""
	Obituary "%o was slaughtered by a mutant Doctor Schabbs."
	Monster
	Bloodtype "Totenkopf_Blood"
	Species "Totenkopf"
	Scale 1.05
	+NOINFIGHTING
	States
	{
		Spawn:
			TK41 A 0
			TK41 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			TK41 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			TK41 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TK41 A 1 A_Look
			Loop
		See:
			TK41 A 0 A_JumpIfInventory("DukeBossSee",1,"See2")
			TK41 A 0 A_JumpIfInventory("CalebBossSee",1,"See2")
			TK41 A 0 A_JumpIfInventory("EleenaBossSee",1,"See2")
			TK41 A 0 A_JumpIfInventory("LoWangBossSee",1,"See2")
			TK41 A 0 A_JumpIfInventory("LeonardBossSee",1,"See2")
			TK41 A 0 A_JumpIfInventory("HLBossSee",1,"See2")
			TK41 A 0 A_GiveInventory("DukeBossSee",1)
			TK41 A 0 A_GiveInventory("CalebBossSee",1)
			TK41 A 0 A_GiveInventory("EleenaBossSee",1)
			TK41 A 0 A_GiveInventory("LoWangBossSee",1)
			TK41 A 0 A_GiveInventory("LeonardBossSee",1)
			TK41 A 0 A_GiveInventory("HLBossSee",1)
			TK41 A 0 A_GiveToTarget("DukeBossSightTaunt")
			TK41 A 0 A_GiveToTarget("CalebBossSightTaunt")
			TK41 A 0 A_GiveToTarget("EleenaBossSightTaunt")
			TK41 A 0 A_GiveToTarget("LoWangBossSightTaunt")
			TK41 A 0 A_GiveToTarget("LeonardBossSightTaunt")
			TK41 A 0 A_GiveToTarget("HLBossSightMusicQueue")
		See2:
			TK41 AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TK41 E 15 A_FaceTarget
			TK41 F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 F 6 Bright A_FaceTarget
			TK41 G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			TK41 G 6 Bright A_FaceTarget
			TK41 F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 F 6 Bright A_FaceTarget
			TK41 G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			TK41 G 6 Bright A_FaceTarget
			TK41 F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 F 6 Bright A_FaceTarget
			TK41 G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			TK41 G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-2,2))
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			TK41 G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			TK41 G 6 Bright A_FaceTarget
			Goto See
		Death:
			TK41 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			TK41 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TK41 H 60 A_Scream
			TK41 I 7
			TK41 J 8 A_NoBlocking
			TK41 K -1 A_KillMaster
			Stop
	}
}

Actor Totenkopf_Tracer_Mutagen : Totenkopf_Tracer_Enemy
{
	Damage (3+(random(0,256)%10))
	DamageType "TotenkopfMutagen"
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 ACS_NamedExecuteWithResult("Totenkopf_Tracer",2,24,40)
			TNT1 A 0 A_SpawnItemEx("Totenkopf_Tracer_Mutagen_Spark",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Totenkopf_Tracer",2,24,40)
			TNT1 A 1 ACS_NamedExecuteWithResult("Totenkopf_BloodGenerator_Offsets")
			TNT1 A 1 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			Stop
	}
}

ACTOR TotenkopfNeedle
{
	Radius 8
	Height 6
	Speed 15
	Damage (random(0,32)>>3)
	Projectile
	SeeSound "Totenkopf/SchabbsThrow"
	DeathSound ""
	Obituary "%o was injected with Doctor Schabb's serum."
	States
	{
		Spawn:
			TK42 ABCD 3
			Loop
		Death:
			TNT1 A 0
			Stop
	}
}

Actor Totenkopf_Tracer_Trail_Mutagen
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK57 AA 1 Bright
			Stop
	}
}

Actor Totenkopf_Tracer_Mutagen_Spark
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK57 BCDE 2 Bright
			Stop
	}
}
